apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.mycompileSdkVersion
    buildToolsVersion project.ext.mybuildToolsVersion

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "GeoPaparazzi_" + android.defaultConfig.versionName + ".apk"
                }
            }
        }
    }
    signingConfigs {
        config {
            def Properties props = new Properties()
            def propFile = new File('geopaparazzi.app/ant.properties')
            if (propFile.canRead()) {
                props.load(new FileInputStream(propFile))
                if (props != null && props.containsKey('key.store') && props.containsKey('key.store.password') &&
                        props.containsKey('key.alias') && props.containsKey('key.alias.password')) {
                    storeFile = file(props['key.store'])
                    storePassword = props['key.store.password']
                    keyAlias = props['key.alias']
                    keyPassword = props['key.alias.password']
                } else {
                    println 'geopaparazzi.app/ant.properties found but some entries are missing'
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                println 'geopaparazzi.app/ant.properties not found'
                android.buildTypes.release.signingConfig = null
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        applicationId project.ext.myApplicationId
        minSdkVersion project.ext.myminSdkVersion
        targetSdkVersion project.ext.mytargetSdkVersion
        versionCode project.ext.myVersionCode
        versionName project.ext.myVersionName
        signingConfig signingConfigs.config
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    api 'com.android.support:appcompat-v7:27.1.0'
    api 'com.android.support:design:27.1.0'
    api 'com.android.support:preference-v7:27.1.0'
    api 'com.android.support:support-v4:27.1.0'
    implementation project(':geopaparazzi_core')
    implementation project(':geopaparazzi_default_import_plugins')
    implementation project(':geopaparazzi_default_export_plugins')
    implementation project(':geopaparazzi_pdf_export_plugins')
    implementation project(':geopaparazzi_projects_import_plugins')
    implementation project(':geopaparazzi_projects_export_plugins')
    implementation project(':geopaparazzi_profiles_import_plugins')
    implementation project(':geopaparazzi_profiles_export_plugins')
}
